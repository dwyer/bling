package cparser

import "C"
import "bling/ast"
import "bling/parser"
import "bling/scanner"
import "bling/token"
import "io/ioutil"
import "runtime"
import "sys"

fun isType(p *parser.Parser) bool {
    switch p.tok {
    case token.ARRAY, token.CONST, token.ENUM, token.EXTERN, token.MAP, token.SIGNED, token.STATIC, token.STRUCT, token.UNION, token.UNSIGNED:
        return true
    case token.IDENT:
        return p.typeNames[p.lit] != nil
    default:
        return false
    }
}

fun parsePragma(p *parser.Parser) *ast.Decl {
    let pos = p.pos
    let lit = p.lit
    p.lit = nil
    p.expect(token.HASH)
    let decl = ast.Decl{
        kind: ast.DECL_PRAGMA,
        pos: pos,
        pragma: {
            lit: lit,
        },
    }
    return &&decl
}

fun postfixExpr(p *parser.Parser, x *ast.Expr) *ast.Expr {
    if x == nil {
        x = p.parseOperand()
    }
    for {
        switch p.tok {
        case token.LBRACK:
            p.expect(token.LBRACK)
            let y = ast.Expr{
                kind: ast.EXPR_INDEX,
                index: {
                    x: x,
                    index: expression(p),
                },
            }
            p.expect(token.RBRACK)
            x = &&y
            break
        case token.LPAREN:
            let args = []*ast.Expr{}
            p.expect(token.LPAREN)
            while p.tok != token.RPAREN {
                var x *ast.Expr = nil
                if isType(p) {
                    x = typeName(p)
                } else {
                    x = expression(p)
                }
                append(args, x)
                if !p.accept(token.COMMA) {
                    break
                }
            }
            p.expect(token.RPAREN)
            let call = ast.Expr{
                kind: ast.EXPR_CALL,
                call: {
                    func: x,
                    args: args,
                },
            }
            x = &&call
            break
        case token.ARROW, token.PERIOD:
            let tok = p.tok
            p.next()
            let y = ast.Expr{
                kind: ast.EXPR_SELECTOR,
                selector: {
                    x: x,
                    tok: tok,
                    sel: p.parseIdent(),
                },
            }
            x = &&y
            break
        default:
            goto done
        }
    }
done:
    return x
}

fun unaryExpr(p *parser.Parser) *ast.Expr {
    let pos = p.pos
    switch p.tok {
    case token.ADD, token.AND, token.BITWISE_NOT, token.NOT, token.SUB:
        let op = p.tok
        p.next()
        let x = ast.Expr{
            kind: ast.EXPR_UNARY,
            unary: {
                pos: pos,
                op: op,
                x: castExpr(p),
            },
        }
        if op == token.LAND {
            p.expect(token.RPAREN)
        }
        return &&x
    case token.MUL:
        p.next()
        let x = ast.Expr{
            kind: ast.EXPR_STAR,
            star: {
                pos: pos,
                x: castExpr(p),
            },
        }
        return &&x
    case token.SIZEOF:
        p.next()
        p.expect(token.LPAREN)
        var x *ast.Expr = nil
        if isType(p) {
            x = typeName(p)
            if p.tok == token.MUL {
                declarator(p, &x)
            }
        } else {
            x = unaryExpr(p)
        }
        p.expect(token.RPAREN)
        let y = ast.Expr{
            kind: ast.EXPR_UNARY,
            unary: {
                pos: pos,
                op: token.SIZEOF,
                x: x,
            },
        }
        return &&y
    case token.DEC, token.INC:
        p.error(p.pos, C.sprintf("unary `%s` not supported in subc", p.tok.string()))
        return nil
    default:
        return postfixExpr(p, nil)
    }
}

fun castExpr(p *parser.Parser) *ast.Expr {
    if p.tok == token.LPAREN {
        let pos = p.expect(token.LPAREN)
        if isType(p) {
            let type = typeName(p)
            p.expect(token.RPAREN)
            if p.tok == token.LBRACE {
                let x = initializer(p)
                x.composite.type = type
                return x
            }
            let y = ast.Expr{
                kind: ast.EXPR_CAST,
                cast: {
                    pos: pos,
                    type: type,
                    expr: castExpr(p),
                },
            }
            return &&y
        } else {
            var x = expression(p)
            if x.kind != ast.EXPR_COMPOSITE_LIT {
                let y = ast.Expr{
                    kind: ast.EXPR_PAREN,
                    paren: {
                        pos: pos,
                        x: x,
                    },
                }
                x = &&y
            }
            p.expect(token.RPAREN)
            return postfixExpr(p, x)
        }
    }
    return unaryExpr(p)
}

fun binaryExpr(p *parser.Parser, prec1 int) *ast.Expr {
    var x = castExpr(p)
    for {
        let op = p.tok
        let oprec = op.precedence()
        if oprec < prec1 {
            return x
        }
        p.expect(op)
        let y = binaryExpr(p, oprec + 1)
        let z = ast.Expr{
            kind: ast.EXPR_BINARY,
            binary: {
                x: x,
                op: op,
                y: y,
            },
        }
        x = &&z
    }
}

fun ternaryExpr(p *parser.Parser) *ast.Expr {
    var x = binaryExpr(p, token.lowest_prec + 1)
    if p.accept(token.QUESTION_MARK) {
        var consequence *ast.Expr = nil
        if p.tok != token.COLON {
            consequence = expression(p)
        }
        p.expect(token.COLON)
        let alternative = ternaryExpr(p)
        let y = ast.Expr{
            kind: ast.EXPR_TERNARY,
            ternary: {
                cond: x,
                x: consequence,
                y: alternative,
            },
        }
        x = &&y
    }
    return x
}

fun expression(p *parser.Parser) *ast.Expr {
    return ternaryExpr(p)
}

fun constantExpr(p *parser.Parser) *ast.Expr {
    return ternaryExpr(p)
}

fun structOrUnionSpec(p *parser.Parser) *ast.Expr {
    let keyword = p.tok
    let pos = p.expect(keyword)
    var name *ast.Expr = nil
    if p.tok == token.IDENT {
        name = p.parseIdent()
    }
    let fields = []*ast.Decl{}
    if p.accept(token.LBRACE) {
        for {
            let pos = p.pos
            let type = specifierQualifierList(p)
            let name = declarator(p, &type)
            let field = ast.Decl{
                kind: ast.DECL_FIELD,
                pos: pos,
                field: {
                    type: type,
                    name: name,
                },
            }
            p.expect(token.SEMICOLON)
            append(fields, &&field)
            if p.tok == token.RBRACE {
                break
            }
        }
        p.expect(token.RBRACE)
    }
    let x = ast.Expr{
        kind: ast.TYPE_STRUCT,
        struct_: {
            pos: pos,
            tok: keyword,
            name: name,
            fields: fields,
        },
    }
    return &&x
}

fun enumSpec(p *parser.Parser) *ast.Expr {
    let pos = p.expect(token.ENUM)
    var name *ast.Expr = nil
    if p.tok == token.IDENT {
        name = p.parseIdent()
    }
    let enums = []*ast.Decl{}
    if p.accept(token.LBRACE) {
        for {
            let decl = ast.Decl{
                kind: ast.DECL_VALUE,
                pos: p.pos,
                value: {
                    name: p.parseIdent(),
                    kind: token.VAR,
                },
            }
            if p.accept(token.ASSIGN) {
                decl.value.value = constantExpr(p)
            }
            let enumerator = &&decl
            append(enums, enumerator)
            if !p.accept(token.COMMA) || p.tok == token.RBRACE {
                break
            }
        }
        p.expect(token.RBRACE)
    }
    let x = ast.Expr{
        kind: ast.TYPE_ENUM,
        enum_: {
            pos: pos,
            name: name,
            enums: enums,
        },
    }
    return &&x
}

fun declarator(p *parser.Parser, typePtr **ast.Expr) *ast.Expr {
    if p.tok == token.MUL {
        *typePtr = pointer(p, *typePtr)
    }
    var name *ast.Expr = nil
    var isPtr = false
    switch p.tok {
    case token.IDENT:
        name = p.parseIdent()
        break
    case token.LPAREN:
        p.expect(token.LPAREN)
        isPtr = p.accept(token.MUL)
        if !isPtr || p.tok == token.IDENT {
            name = p.parseIdent()
        }
        p.expect(token.RPAREN)
        break
    default:
        break
    }
    if p.tok == token.LBRACK {
        let pos token.Pos = p.pos
        p.next()
        var len *ast.Expr = nil
        if p.tok != token.RBRACK {
            len = constantExpr(p)
        }
        let type = ast.Expr{
            kind: ast.TYPE_ARRAY,
            array_: {
                pos: pos,
                elt: *typePtr,
                len: len,
            },
        }
        p.expect(token.RBRACK)
        *typePtr = &&type
    } else if p.accept(token.LPAREN) {
        var params = []*ast.Decl{}
        if p.tok != token.RPAREN {
            params = paramTypeList(p)
        }
        let type = ast.Expr{
            kind: ast.TYPE_FUNC,
            func: {
                pos: (*typePtr).pos(),
                result: *typePtr,
                params: params,
            },
        }
        p.expect(token.RPAREN)
        *typePtr = &&type
    }
    if isPtr {
        let type = ast.Expr{
            kind: ast.EXPR_STAR,
            star: {
                x: *typePtr,
            },
        }
        *typePtr = &&type
    }
    return name
}

fun typeQualifier(p *parser.Parser, type *ast.Expr) *ast.Expr {
    if p.accept(token.CONST) {
        type.isConst = true
    }
    return type
}

fun pointer(p *parser.Parser, type *ast.Expr) *ast.Expr {
    while p.tok == token.MUL {
        let x = ast.Expr{
            kind: ast.EXPR_STAR,
            star: {
                pos: p.pos,
                x: type,
            },
        }
        p.next()
        type = &&x
        type = typeQualifier(p, type)
    }
    return type
}

fun paramTypeList(p *parser.Parser) []*ast.Decl {
    let params = []*ast.Decl{}
    while p.tok != token.RPAREN {
        let param = parameterDeclaration(p)
        append(params, param)
        if !p.accept(token.COMMA) {
            break
        }
        if p.tok == token.ELLIPSIS {
            let pos = p.expect(token.ELLIPSIS)
            let type = ast.Expr{
                kind: ast.TYPE_ELLIPSIS,
                ellipsis: {
                    pos: pos,
                },
            }
            let decl = ast.Decl{
                kind: ast.DECL_FIELD,
                pos: pos,
                field: {
                    type: &&type,
                },
            }
            let param = &&decl
            append(params, param)
            break
        }
    }
    return params
}

fun typeName(p *parser.Parser) *ast.Expr {
    var type = specifierQualifierList(p)
    let decl = abstractDeclarator(p, type)
    type = decl.field.type
    delete(decl)
    return type
}

fun abstractDeclarator(p *parser.Parser, type *ast.Expr) *ast.Decl {
    let pos = p.pos
    if p.tok == token.MUL {
        type = pointer(p, type)
    }
    var isPtr = false
    if p.accept(token.LPAREN) {
        p.expect(token.MUL)
        p.expect(token.RPAREN)
        isPtr = true
    }
    for {
        if p.accept(token.LBRACK) {
            var len *ast.Expr = nil
            if p.tok != token.RBRACK {
                len = constantExpr(p)
            }
            let t = ast.Expr{
                kind: ast.TYPE_ARRAY,
                array_: {
                    pos: pos,
                    elt: type,
                    len: len,
                },
            }
            p.expect(token.RBRACK)
            type = &&t
        } else if p.accept(token.LPAREN) {
            var params = []*ast.Decl{}
            if p.tok != token.RPAREN {
                params = paramTypeList(p)
            }
            let t = ast.Expr{
                kind: ast.TYPE_FUNC,
                func: {
                    pos: pos,
                    result: type,
                    params: params,
                },
            }
            p.expect(token.RPAREN)
            type = &&t
        } else {
            break
        }
    }
    if isPtr {
        let tmp = ast.Expr{
            kind: ast.EXPR_STAR,
            star: {
                pos: pos,
                x: type,
            },
        }
        type = &&tmp
    }
    let declarator = ast.Decl{
        kind: ast.DECL_FIELD,
        pos: pos,
        field: {
            type: type,
        },
    }
    return &&declarator
}

fun initializer(p *parser.Parser) *ast.Expr {
    if !p.accept(token.LBRACE) {
        return expression(p)
    }
    let list = []*ast.Expr{}
    while p.tok != token.RBRACE && p.tok != token.EOF {
        var key *ast.Expr = nil
        var isArray = false
        if p.accept(token.PERIOD) {
            key = p.parseIdent()
            p.expect(token.ASSIGN)
        } else if p.accept(token.LBRACK) {
            isArray = true
            key = expression(p)
            p.expect(token.RBRACK)
            p.expect(token.ASSIGN)
        }
        var value = initializer(p)
        if key {
            let x = ast.Expr{
                kind: ast.EXPR_KEY_VALUE,
                keyValue: {
                    key: key,
                    value: value,
                    isArray: isArray,
                },
            }
            value = &&x
        }
        append(list, value)
        if !p.accept(token.COMMA) {
            break
        }
    }
    let expr = ast.Expr{
        kind: ast.EXPR_COMPOSITE_LIT,
        composite: {
            pos: p.expect(token.RBRACE),
            list: list,
        },
    }
    return &&expr
}

fun simpleStmt(p *parser.Parser, labelOk bool) *ast.Stmt {
    let x = expression(p)
    let op = p.tok
    switch op {
    case token.ADD_ASSIGN, token.ASSIGN, token.DIV_ASSIGN, token.MOD_ASSIGN, token.MUL_ASSIGN, token.SHL_ASSIGN, token.SHR_ASSIGN, token.SUB_ASSIGN, token.XOR_ASSIGN:
        p.next()
        let y = expression(p)
        let stmt = ast.Stmt{
            kind: ast.STMT_ASSIGN,
            assign: {
                x: x,
                op: op,
                y: y,
            },
        }
        return &&stmt
    case token.INC, token.DEC:
        p.next()
        let stmt = ast.Stmt{
            kind: ast.STMT_POSTFIX,
            postfix: {
                x: x,
                op: op,
            },
        }
        return &&stmt
    default:
        break
    }
    if labelOk && x.kind == ast.EXPR_IDENT {
        if p.accept(token.COLON) {
            let stmt = ast.Stmt{
                kind: ast.STMT_LABEL,
                label: {
                    label: x,
                    stmt: statement(p),
                },
            }
            return &&stmt
        }
    }
    let stmt = ast.Stmt{
        kind: ast.STMT_EXPR,
        expr: {
            x: x,
        },
    }
    return &&stmt
}

fun statement(p *parser.Parser) *ast.Stmt {
    if isType(p) {
        let stmt = ast.Stmt{
            kind: ast.STMT_DECL,
            decl: {
                decl: declaration(p, false),
            },
        }
        return &&stmt
    }
    let pos = p.pos
    if p.accept(token.FOR) {
        p.expect(token.LPAREN)
        var init *ast.Stmt = nil
        var cond *ast.Expr = nil
        var post *ast.Stmt = nil
        if !p.accept(token.SEMICOLON) {
            init = statement(p)
        }
        if p.tok != token.SEMICOLON {
            cond = expression(p)
        }
        p.expect(token.SEMICOLON)
        if p.tok != token.RPAREN {
            post = simpleStmt(p, false)
        }
        p.expect(token.RPAREN)
        let body = compoundStmt(p, true)
        let stmt = ast.Stmt{
            kind: ast.STMT_ITER,
            iter: {
                pos: pos,
                kind: token.FOR,
                init: init,
                cond: cond,
                post: post,
                body: body,
            },
        }
        return &&stmt
    }
    if p.accept(token.IF) {
        p.expect(token.LPAREN)
        let cond = expression(p)
        p.expect(token.RPAREN)
        let body = compoundStmt(p, true)
        var else_ *ast.Stmt = nil
        if p.accept(token.ELSE) {
            if p.tok == token.IF {
                else_ = statement(p)
            } else {
                else_ = compoundStmt(p, true)
            }
        }
        let stmt = ast.Stmt{
            kind: ast.STMT_IF,
            if_: {
                pos: pos,
                cond: cond,
                body: body,
                else_: else_,
            },
        }
        return &&stmt
    }
    if p.accept(token.RETURN) {
        var x *ast.Expr = nil
        if p.tok != token.SEMICOLON {
            x = expression(p)
        }
        p.expect(token.SEMICOLON)
        let stmt = ast.Stmt{
            kind: ast.STMT_RETURN,
            return_: {
                pos: pos,
                x: x,
            },
        }
        return &&stmt
    }
    if p.accept(token.SWITCH) {
        p.expect(token.LPAREN)
        let tag = expression(p)
        p.expect(token.RPAREN)
        p.expect(token.LBRACE)
        let clauses = []*ast.Stmt{}
        while p.tok == token.CASE || p.tok == token.DEFAULT {
            let exprs = []*ast.Expr{}
            while p.accept(token.CASE) {
                let expr = constantExpr(p)
                p.expect(token.COLON)
                append(exprs, expr)
            }
            if len(exprs) == 0 {
                p.expect(token.DEFAULT)
                p.expect(token.COLON)
            }
            let stmts = []*ast.Stmt{}
            for var loop = true; loop; {
                switch p.tok {
                case token.CASE, token.DEFAULT, token.RBRACE:
                    loop = false
                    break
                default:
                    break
                }
                if loop {
                    append(stmts, statement(p))
                }
            }
            let stmt = ast.Stmt{
                kind: ast.STMT_CASE,
                case_: {
                    pos: pos,
                    exprs: exprs,
                    stmts: stmts,
                },
            }
            let clause = &&stmt
            append(clauses, clause)
        }
        p.expect(token.RBRACE)
        let stmt = ast.Stmt{
            kind: ast.STMT_SWITCH,
            switch_: {
                pos: pos,
                tag: tag,
                stmts: clauses,
            },
        }
        return &&stmt
    }
    if p.accept(token.WHILE) {
        p.expect(token.LPAREN)
        let cond = expression(p)
        p.expect(token.RPAREN)
        let body = compoundStmt(p, true)
        let stmt = ast.Stmt{
            kind: ast.STMT_ITER,
            iter: {
                pos: pos,
                kind: token.WHILE,
                cond: cond,
                body: body,
            },
        }
        return &&stmt
    }
    switch p.tok {
    case token.BREAK, token.CONTINUE, token.FALLTHROUGH, token.GOTO:
        let keyword = p.tok
        p.next()
        var label *ast.Expr = nil
        if keyword == token.GOTO {
            label = p.parseIdent()
        }
        p.expect(token.SEMICOLON)
        let stmt = ast.Stmt{
            kind: ast.STMT_JUMP,
            jump: {
                pos: pos,
                keyword: keyword,
                label: label,
            },
        }
        return &&stmt
    case token.LBRACE:
        return compoundStmt(p, false)
    default:
        break
    }
    if p.accept(token.SEMICOLON) {
        let stmt = ast.Stmt{
            kind: ast.STMT_EMPTY,
            empty: {
                pos: pos,
            },
        }
        return &&stmt
    }
    let stmt = simpleStmt(p, true)
    if stmt.kind != ast.STMT_LABEL {
        p.expect(token.SEMICOLON)
    }
    return stmt
}

fun compoundStmt(p *parser.Parser, allow_single bool) *ast.Stmt {
    let stmts = []*ast.Stmt{}
    var pos token.Pos = 0
    if allow_single && p.tok != token.LBRACE {
        let stmt = statement(p)
        assert(stmt.kind != ast.STMT_DECL)
        append(stmts, stmt)
        pos = stmt.pos()
    } else {
        pos = p.expect(token.LBRACE)
        while p.tok != token.RBRACE {
            append(stmts, statement(p))
        }
        p.expect(token.RBRACE)
    }
    let stmt = ast.Stmt{
        kind: ast.STMT_BLOCK,
        block: {
            pos: pos,
            stmts: stmts,
        },
    }
    return &&stmt
}

fun parameterDeclaration(p *parser.Parser) *ast.Decl {
    let decl = ast.Decl{
        kind: ast.DECL_FIELD,
        pos: p.pos,
    }
    decl.field.type = declarationSpecifiers(p, false)
    decl.field.name = declarator(p, &decl.field.type)
    return &&decl
}

fun arraySpecifier(p *parser.Parser) *ast.Expr {
    let pos = p.pos
    p.expect(token.ARRAY)
    p.expect(token.LPAREN)
    let elt = typeName(p)
    p.expect(token.RPAREN)
    let tmp = ast.Expr{
        kind: ast.TYPE_ARRAY,
        array_: {
            pos: pos,
            elt: elt,
            dynamic: true,
        },
    }
    return &&tmp
}

fun mapSpecifier(p *parser.Parser) *ast.Expr {
    let pos = p.pos
    p.expect(token.MAP)
    p.expect(token.LPAREN)
    let val = typeName(p)
    p.expect(token.RPAREN)
    let tmp = ast.Expr{
        kind: ast.TYPE_MAP,
        map_: {
            pos: pos,
            val: val,
        },
    }
    return &&tmp
}

fun typeSpecifier(p *parser.Parser) *ast.Expr {
    var x *ast.Expr = nil
    switch p.tok {
    case token.SIGNED, token.UNSIGNED:
        p.error(p.pos, C.sprintf("`%s` is not supported in subc", p.tok.string()))
        break
    case token.STRUCT, token.UNION:
        x = structOrUnionSpec(p)
        break
    case token.ENUM:
        x = enumSpec(p)
        break
    case token.ARRAY:
        x = arraySpecifier(p)
        break
    case token.MAP:
        x = mapSpecifier(p)
        break
    default:
        if isType(p) {
            x = p.parseIdent()
        } else {
            p.errorExpected(p.pos, "type")
        }
        break
    }
    return x
}

fun declarationSpecifiers(p *parser.Parser, isTop bool) *ast.Expr {
    if isTop {
        switch p.tok {
        case token.EXTERN, token.STATIC:
            p.next()
            break
        default:
            break
        }
    }
    let isConst = p.accept(token.CONST)
    let type = typeSpecifier(p)
    if isConst {
        type.isConst = isConst
    }
    return type
}

fun specifierQualifierList(p *parser.Parser) *ast.Expr {
    return declarationSpecifiers(p, false)
}

fun declaration(p *parser.Parser, isExternal bool) *ast.Decl {
    if p.tok == token.HASH {
        return parsePragma(p)
    }
    let pos token.Pos = p.pos
    if p.tok == token.TYPEDEF {
        let keyword = p.tok
        p.expect(keyword)
        let type = declarationSpecifiers(p, true)
        let name = declarator(p, &type)
        p.expect(token.SEMICOLON)
        let decl = ast.Decl{
            kind: ast.DECL_TYPE,
            pos: pos,
            type: {
                name: name,
                type: type,
            },
        }
        p.typeNames[name.ident.name] = type
        return &&decl
    }
    let type = declarationSpecifiers(p, true)
    var name = declarator(p, &type)
    var value *ast.Expr = nil
    if type.kind == ast.TYPE_FUNC {
        let decl = ast.Decl{
            kind: ast.DECL_FUNC,
            pos: pos,
            func: {
                type: type,
                name: name,
            },
        }
        if isExternal && p.tok == token.LBRACE {
            decl.func.body = compoundStmt(p, false)
        } else {
            p.expect(token.SEMICOLON)
        }
        return &&decl
    }
    if p.accept(token.ASSIGN) {
        value = initializer(p)
    }
    p.expect(token.SEMICOLON)
    if name != nil {
        let decl = ast.Decl{
            kind: ast.DECL_VALUE,
            pos: pos,
            value: {
                type: type,
                name: name,
                value: value,
                kind: token.VAR,
            },
        }
        return &&decl
    } else {
        switch type.kind {
        case ast.TYPE_STRUCT:
            name = type.struct_.name
            break
        default:
            panic(C.sprintf("bad kind: %d", type.kind))
            break
        }
        let decl = ast.Decl{
            kind: ast.DECL_TYPE,
            pos: pos,
            type: {
                type: type,
                name: name,
            },
        }
        return &&decl
    }
}

fun _parseFile(p *parser.Parser) *ast.File {
    let decls = []*ast.Decl{}
    let imports = []*ast.Decl{}
    var name *ast.Expr = nil
    while p.tok == token.HASH {
        let lit = parsePragma(p)
        append(decls, lit)
    }
    if p.accept(token.PACKAGE) {
        p.expect(token.LPAREN)
        name = p.parseIdent()
        p.expect(token.RPAREN)
        p.expect(token.SEMICOLON)
    }
    while p.tok == token.IMPORT {
        p.expect(token.IMPORT)
        p.expect(token.LPAREN)
        let path = p.parseBasicLit(token.STRING)
        p.expect(token.RPAREN)
        p.expect(token.SEMICOLON)
        let decl = ast.Decl{
            kind: ast.DECL_IMPORT,
            imp: {
                path: path,
            },
        }
        append(imports, &&decl)
    }
    while p.tok != token.EOF {
        append(decls, declaration(p, true))
    }
    let file = ast.File{
        filename: p.file.name,
        name: name,
        decls: decls,
        imports: imports,
    }
    return &&file
}

fun parseFile(fset *token.FileSet, filename string) *ast.File {
    let src = ioutil.readFile(filename, nil)
    let p = parser.Parser{}
    p.init(fset, filename, src)
    p.cMode = true
    let file = _parseFile(&p)
    delete(src)
    return file
}
