package types

import "bling/ast"
import "bling/emitter"
import "bling/parser"
import "bling/token"
import "sys"
import "utils"

typ Config struct {
    strict bool
    cMode bool
    ignoreFuncBodies bool
}

typ Package struct {
    path *char
    name *char
    scope *ast.Scope
    imports utils.Slice
}

typ Info struct {
    imports map Package
}

typ BasicKind enum {
    INVALID
    VOID
    BOOL
    CHAR
    INT
    INT8
    INT16
    INT32
    INT64
    UINT
    UINT8
    UINT16
    UINT32
    UINT64
    UINTPTR
    FLOAT32
    FLOAT64
    VOID_POINTER
    UNSAFE_POINTER
}

typ BasicInfo enum {
    IS_BOOLEAN = 1 << 0
    IS_INTEGER = 1 << 1
    IS_UNSIGNED = 1 << 2
    IS_FLOAT = 1 << 3
    IS_COMPLEX = 1 << 4
    IS_STRING = 1 << 5
    IS_UNTYPED = 1 << 6
    IS_ORDERED = IS_INTEGER | IS_FLOAT | IS_STRING
    IS_NUMERIC = IS_INTEGER | IS_FLOAT | IS_COMPLEX
    IS_CONST_EXPR = IS_BOOLEAN | IS_NUMERIC | IS_STRING
}

typ Basic struct {
    kind BasicKind
    info BasicInfo
    name *char
}

fun constant_stringVal(x *ast.Expr) *char {
    assert(x.kind == ast.EXPR_BASIC_LIT)
    var lit *const char = x.basic.value
    var n int = sys.strlen(lit) - 2
    var val *char = sys.malloc(n + 1)
    for var i int = 0; i < n; i++ {
        val[i] = lit[i + 1]
    }
    val[n] = '\0'
    return val
}

fun declString(decl *ast.Decl) *char {
    var e emitter.Emitter = {}
    emitter.emitDecl(&e, decl)
    return emitter.Emitter_string(&e)
}

fun exprString(expr *ast.Expr) *char {
    var e emitter.Emitter = {}
    emitter.emitExpr(&e, expr)
    return emitter.Emitter_string(&e)
}

fun stmtString(stmt *ast.Stmt) *char {
    var e emitter.Emitter = {}
    emitter.emitStmt(&e, stmt)
    return emitter.Emitter_string(&e)
}

fun typeString(expr *ast.Expr) *char {
    var e emitter.Emitter = {}
    emitter.emitType(&e, expr)
    return emitter.Emitter_string(&e)
}

fun isType(expr *ast.Expr) bool {
    switch expr.kind {
    case ast.EXPR_IDENT:
        if expr.ident.obj == NULL {
            panic(sys.sprintf("types$isType: unresolved identifier %s", expr.ident.name))
        }
        return expr.ident.obj.decl.kind == ast.DECL_TYPEDEF
    case ast.EXPR_STAR:
        return isType(expr.star.x)
    case ast.TYPE_ARRAY, ast.TYPE_ENUM, ast.TYPE_FUNC, ast.TYPE_NATIVE, ast.TYPE_STRUCT:
        return true
    default:
        return false
    }
}

fun makeIdent(name *const char) *ast.Expr {
    var x ast.Expr = {
        kind: ast.EXPR_IDENT,
        ident: {
            name: sys.strdup(name),
        },
    }
    return &&x
}

fun makePtr(type *ast.Expr) *ast.Expr {
    var x ast.Expr = {
        kind: ast.EXPR_STAR,
        star: {
            x: type,
        },
    }
    return &&x
}

fun getUnderlyingType(ident *ast.Expr) *ast.Expr {
    if ident.ident.obj == NULL {
        panic(sys.sprintf("not resolved: %s", typeString(ident)))
    }
    var decl *ast.Decl = ident.ident.obj.decl
    if decl.kind != ast.DECL_TYPEDEF {
        panic(sys.sprintf("not a type: %s", typeString(ident)))
    }
    return decl.typedef_.type
}

fun getBaseType(type *ast.Expr) *ast.Expr {
    for ;; {
        switch type.kind {
        case ast.EXPR_IDENT:
            type = getUnderlyingType(type)
            break
        case ast.EXPR_SELECTOR:
            type = type.selector.sel
            break
        case ast.TYPE_ARRAY, ast.TYPE_ENUM, ast.TYPE_MAP, ast.TYPE_NATIVE, ast.TYPE_STRUCT:
            return type
        default:
            panic(sys.sprintf("not a type: %s", typeString(type)))
        }
    }
}

fun isArithmetic(type *ast.Expr) bool {
    switch type.kind {
    case ast.EXPR_IDENT:
        return isArithmetic(getBaseType(type))
    case ast.EXPR_STAR, ast.TYPE_ENUM:
        return true
    case ast.TYPE_NATIVE:
        return !sys.streq(type.native.name, "void")
    default:
        return false
    }
}

fun isNative(type *ast.Expr, name *const char) bool {
    switch type.kind {
    case ast.EXPR_IDENT:
        return sys.streq(type.ident.name, name)
    case ast.TYPE_NATIVE:
        return true
    default:
        return false
    }
}

fun areIdentical(a *ast.Expr, b *ast.Expr) bool {
    if a == b {
        return true
    }
    if a == NULL || b == NULL {
        return false
    }
    if a.kind == ast.TYPE_MAP || b.kind == ast.TYPE_MAP {
        return true
    }
    assert(a)
    assert(b)
    if a.kind == ast.EXPR_SELECTOR {
        a = a.selector.sel
    }
    if b.kind == ast.EXPR_SELECTOR {
        b = b.selector.sel
    }
    if a.kind != b.kind {
        return false
    }
    switch a.kind {
    case ast.EXPR_IDENT:
        return b.kind == ast.EXPR_IDENT && a.ident.obj == a.ident.obj
    case ast.EXPR_STAR:
        return areIdentical(a.star.x, b.star.x)
    case ast.TYPE_ARRAY:
        return areIdentical(a.array.elt, b.array.elt)
    case ast.TYPE_FUNC:
        {
            if !areIdentical(a.func.result, b.func.result) {
                return false
            }
            var i int = 0
            if a.func.params {
                for ; a.func.params[i]; i++ {
                    var param1 *ast.Decl = a.func.params[i]
                    var param2 *ast.Decl = b.func.params[i]
                    if param2 == NULL {
                        return false
                    }
                    if !areIdentical(param1.field.type, param2.field.type) {
                        return false
                    }
                }
                if b.func.params[i] {
                    return false
                }
            }
        }
        return true
    default:
        panic(sys.sprintf("not implemented: %s == %s", typeString(a), typeString(b)))
        return false
    }
}

fun isPointer(t *ast.Expr) bool {
    return t.kind == ast.EXPR_STAR || t.kind == ast.TYPE_ARRAY
}

fun pointerBase(t *ast.Expr) *ast.Expr {
    switch t.kind {
    case ast.EXPR_STAR:
        return t.star.x
    case ast.TYPE_ARRAY:
        return t.array.elt
    default:
        panic(sys.sprintf("not a pointer: %s", typeString(t)))
        return NULL
    }
}

fun areAssignable(a *ast.Expr, b *ast.Expr) bool {
    if a.kind == ast.TYPE_ELLIPSIS {
        return true
    }
    if areIdentical(a, b) {
        return true
    }
    if ast.isVoidPtr(a) || ast.isVoidPtr(b) {
        return true
    }
    if isPointer(a) && isPointer(b) {
        return areAssignable(pointerBase(a), pointerBase(b))
    }
    while a.kind == ast.EXPR_IDENT {
        a = getUnderlyingType(a)
    }
    while b.kind == ast.EXPR_IDENT {
        b = getUnderlyingType(b)
    }
    if isNative(a, "bool") && isArithmetic(b) {
        return true
    }
    if b.kind == ast.TYPE_ENUM && isArithmetic(a) {
        return true
    }
    if a.kind == ast.TYPE_ENUM && isArithmetic(a) {
        return true
    }
    return areIdentical(a, b)
}

fun areComparable(a *ast.Expr, b *ast.Expr) bool {
    if areIdentical(a, b) {
        return true
    }
    if isArithmetic(a) && isArithmetic(b) {
        return true
    }
    if a.kind == ast.EXPR_STAR && isNative(b, "int") {
        return true
    }
    return areIdentical(a, b)
}

fun isLhs(expr *ast.Expr) bool {
    switch expr.kind {
    case ast.EXPR_IDENT, ast.EXPR_SELECTOR:
        return true
    case ast.EXPR_CAST:
        return isLhs(expr.cast.expr)
    case ast.EXPR_INDEX:
        return isLhs(expr.index.x)
    case ast.EXPR_PAREN:
        return isLhs(expr.paren.x)
    case ast.EXPR_STAR:
        return isLhs(expr.star.x)
    default:
        return false
    }
}

fun getDeclType(decl *ast.Decl) *ast.Expr {
    assert(decl)
    switch decl.kind {
    case ast.DECL_FIELD:
        return decl.field.type
    case ast.DECL_FUNC:
        return decl.func.type
    case ast.DECL_IMPORT:
        return NULL
    case ast.DECL_TYPEDEF:
        return decl.typedef_.type
    case ast.DECL_VALUE:
        return decl.value.type
    default:
        panic(sys.sprintf("unhandled decl: %s", declString(decl)))
        return NULL
    }
}

fun getStructFieldByName(type *ast.Expr, name *ast.Expr) *ast.Decl {
    var base *ast.Expr = getBaseType(type)
    assert(base.kind == ast.TYPE_STRUCT)
    assert(name.kind == ast.EXPR_IDENT)
    for var i int = 0; base.struct_.fields && base.struct_.fields[i]; i++ {
        var field *ast.Decl = base.struct_.fields[i]
        if field.field.name {
            if sys.streq(name.ident.name, field.field.name.ident.name) {
                base = field.field.type
                return field
            }
        } else {
            var subField *ast.Decl = getStructFieldByName(field.field.type, name)
            if subField {
                return subField
            }
        }
    }
    return NULL
}

fun getStructField(type *ast.Expr, index int) *ast.Decl {
    var base *ast.Expr = getBaseType(type)
    if base.kind != ast.TYPE_STRUCT {
        panic(sys.sprintf("not a struct: %s", typeString(type)))
    }
    if base.struct_.fields == NULL {
        panic(sys.sprintf("incomplete field defn: %s", typeString(type)))
    }
    for var i int = 0; base.struct_.fields[i]; i++ {
        if i == index {
            return base.struct_.fields[i]
        }
    }
    return NULL
}

typ Checker struct {
    conf *Config
    fset *token.FileSet
    pkg *Package
    info *Info
    result *ast.Expr
}

fun Checker_error(c *Checker, pos token.Pos, msg *const char) {
    token.FileSet_print(c.fset)
    var position token.Position = {}
    var file *token.File = token.FileSet_file(c.fset, pos)
    if file {
        position = token.File_position(file, pos)
    }
    panic(sys.sprintf("%s: %s\n%s", token.Position_string(&position), msg, token.File_lineString(file, position.line)))
}

fun Checker_resolve(c *Checker, s *ast.Scope, x *ast.Expr) {
    assert(x.kind == ast.EXPR_IDENT)
    if x.ident.obj {
        Checker_error(c, ast.Expr_pos(x), "already resolved")
    }
    if ast.resolve(s, x) {
        return
    }
    Checker_error(c, ast.Expr_pos(x), sys.sprintf("unresolved: %s", x.ident.name))
}

fun Checker_declare(c *Checker, decl *ast.Decl, data *void, scope *ast.Scope, kind ast.ObjKind, ident *ast.Expr) {
    if ident.ident.obj != NULL {
        Checker_error(c, decl.pos, sys.sprintf("already declared: %s", ident.ident.name))
    }
    var obj *ast.Object = ast.newObject(kind, ident.ident.name)
    obj.decl = decl
    obj.data = data
    obj.scope = scope
    ident.ident.obj = obj
    var alt *ast.Object = ast.Scope_insert(scope, obj)
    if alt != NULL {
        Checker_error(c, decl.pos, sys.sprintf("incompatible redefinition of `%s`: %s", declString(obj.decl), declString(decl)))
    }
}

fun Checker_openScope(c *Checker) {
    c.pkg.scope = ast.Scope_new(c.pkg.scope)
}

fun Checker_closeScope(c *Checker) {
    c.pkg.scope = c.pkg.scope.outer
}

fun Checker_checkType(c *Checker, t *ast.Expr) {
    assert(t)
    switch t.kind {
    case ast.EXPR_IDENT:
        Checker_resolve(c, c.pkg.scope, t)
        break
    case ast.EXPR_SELECTOR:
        {
            var type *ast.Expr = Checker_checkExpr(c, t.selector.x)
            assert(type == NULL)
            assert(ast.isIdent(t.selector.x))
            assert(t.selector.x.ident.obj.kind == ast.ObjKind_PKG)
            var oldScope *ast.Scope = c.pkg.scope
            c.pkg.scope = t.selector.x.ident.obj.data
            Checker_checkType(c, t.selector.sel)
            c.pkg.scope = oldScope
        }
        break
    case ast.EXPR_STAR:
        Checker_checkType(c, t.star.x)
        break
    case ast.TYPE_ARRAY:
        Checker_checkType(c, t.array.elt)
        if t.array.len {
            var len *ast.Expr = Checker_checkExpr(c, t.array.len)
            typ void(len)
        }
        break
    case ast.TYPE_ELLIPSIS:
        break
    case ast.TYPE_ENUM:
        for var i int = 0; t.enum_.enums[i]; i++ {
            var decl *ast.Decl = t.enum_.enums[i]
            if t.enum_.name {
                decl.value.type = t.enum_.name
            }
            Checker_declare(c, decl, NULL, c.pkg.scope, ast.ObjKind_CON, decl.value.name)
            if decl.value.value {
                Checker_checkExpr(c, decl.value.value)
            }
        }
        break
    case ast.TYPE_FUNC:
        for var i int = 0; t.func.params && t.func.params[i]; i++ {
            var param *ast.Decl = t.func.params[i]
            assert(param.kind == ast.DECL_FIELD)
            Checker_checkType(c, param.field.type)
        }
        if t.func.result {
            Checker_checkType(c, t.func.result)
        }
        break
    case ast.TYPE_MAP:
        if t.map_.val {
            Checker_checkType(c, t.map_.val)
        }
        break
    case ast.TYPE_STRUCT:
        if t.struct_.fields {
            Checker_openScope(c)
            for var i int = 0; t.struct_.fields[i]; i++ {
                var field *ast.Decl = t.struct_.fields[i]
                Checker_checkType(c, field.field.type)
                if field.field.name {
                    Checker_declare(c, field, NULL, c.pkg.scope, ast.ObjKind_VAL, field.field.name)
                }
            }
            Checker_closeScope(c)
        }
        break
    default:
        Checker_error(c, ast.Expr_pos(t), "unknown type")
        break
    }
}

fun Checker_checkIdent(c *Checker, expr *ast.Expr) *ast.Expr {
    assert(expr.kind == ast.EXPR_IDENT)
    if expr.ident.obj == NULL {
        Checker_error(c, ast.Expr_pos(expr), "unresolved identifier")
    }
    return getDeclType(expr.ident.obj.decl)
}

fun isInteger(x *ast.Expr) bool {
    switch x.kind {
    case ast.EXPR_IDENT:
        return isInteger(x.ident.obj.decl.typedef_.type)
    case ast.TYPE_ENUM, ast.TYPE_NATIVE:
        return true
    default:
        return false
    }
}

fun Checker_checkArrayLit(c *Checker, x *ast.Expr) {
    var baseT *ast.Expr = getBaseType(x.composite.type)
    for var i int = 0; x.composite.list[i]; i++ {
        var elt *ast.Expr = x.composite.list[i]
        if elt.kind == ast.EXPR_KEY_VALUE {
            elt.key_value.isArray = true
            var indexT *ast.Expr = Checker_checkExpr(c, elt.key_value.key)
            if !isInteger(indexT) {
                Checker_error(c, ast.Expr_pos(elt), sys.sprintf("not a valid index: %s", exprString(elt.key_value.key)))
            }
            elt = elt.key_value.value
        }
        if elt.kind == ast.EXPR_COMPOSITE_LIT {
            if elt.composite.type == NULL {
                elt.composite.type = baseT.array.elt
            } else {
                Checker_checkType(c, elt.composite.type)
            }
            Checker_checkCompositeLit(c, elt)
        } else {
            Checker_checkExpr(c, elt)
        }
    }
}

fun Checker_checkStructLit(c *Checker, x *ast.Expr) {
    assert(x.composite.type)
    var expectKV bool = false
    for var i int = 0; x.composite.list[i]; i++ {
        var elt *ast.Expr = x.composite.list[i]
        var fieldT *ast.Expr = NULL
        if elt.kind == ast.EXPR_KEY_VALUE {
            elt.key_value.isArray = false
            expectKV = true
            var key *ast.Expr = elt.key_value.key
            if !ast.isIdent(key) {
                Checker_error(c, ast.Expr_pos(x), "key must be an identifier")
            }
            var field *ast.Decl = getStructFieldByName(x.composite.type, key)
            if field == NULL {
                Checker_error(c, ast.Expr_pos(key), sys.sprintf("no member named '%s' in '%s'", exprString(key), typeString(x.composite.type)))
            }
            key.ident.obj = field.field.name.ident.obj
            fieldT = field.field.type
            elt = elt.key_value.value
        } else {
            if expectKV {
                Checker_error(c, ast.Expr_pos(x), "expected a key/value expr")
            }
            var field *ast.Decl = getStructField(x.composite.type, i)
            fieldT = field.field.type
        }
        var eltT *ast.Expr = NULL
        if elt.kind == ast.EXPR_COMPOSITE_LIT {
            if elt.composite.type == NULL {
                elt.composite.type = fieldT
            } else {
                Checker_checkType(c, elt.composite.type)
            }
            eltT = Checker_checkCompositeLit(c, elt)
        } else {
            eltT = Checker_checkExpr(c, elt)
        }
        if !areAssignable(fieldT, eltT) {
            Checker_error(c, ast.Expr_pos(elt), sys.sprintf("cannot init field of type `%s` with value of type `%s`", typeString(fieldT), typeString(eltT)))
        }
    }
}

fun Checker_checkCompositeLit(c *Checker, x *ast.Expr) *ast.Expr {
    var t *ast.Expr = x.composite.type
    assert(t)
    var baseT *ast.Expr = getBaseType(t)
    switch baseT.kind {
    case ast.TYPE_ARRAY:
        Checker_checkArrayLit(c, x)
        break
    case ast.TYPE_STRUCT:
        Checker_checkStructLit(c, x)
        break
    default:
        Checker_error(c, ast.Expr_pos(x), "composite type must be an array or a struct")
        break
    }
    return t
}

fun Checker_lookupIdent(c *Checker, name *char) *ast.Expr {
    var obj *ast.Object = ast.Scope_deepLookup(c.pkg.scope, name)
    assert(obj)
    assert(obj.kind == ast.ObjKind_TYP)
    assert(obj.decl.kind == ast.DECL_TYPEDEF)
    var t *ast.Expr = obj.decl.typedef_.name
    assert(t.ident.obj)
    return t
}

fun Checker_checkExpr(c *Checker, expr *ast.Expr) *ast.Expr {
    assert(expr)
    switch expr.kind {
    case ast.EXPR_BINARY:
        {
            var typ1 *ast.Expr = Checker_checkExpr(c, expr.binary.x)
            var typ2 *ast.Expr = Checker_checkExpr(c, expr.binary.y)
            if !areComparable(typ1, typ2) {
                Checker_error(c, ast.Expr_pos(expr), sys.sprintf("not compariable: %s and %s", typeString(typ1), typeString(typ2)))
            }
            switch expr.binary.op {
            case token.EQUAL, token.GT, token.GT_EQUAL, token.LAND, token.LOR, token.LT, token.LT_EQUAL, token.NOT_EQUAL:
                return Checker_lookupIdent(c, "bool")
            default:
                return typ1
            }
        }
    case ast.EXPR_BASIC_LIT:
        switch expr.basic.kind {
        case token.CHAR:
            return Checker_lookupIdent(c, "char")
        case token.FLOAT:
            return Checker_lookupIdent(c, "float")
        case token.INT:
            return Checker_lookupIdent(c, "int")
        case token.STRING:
            return makePtr(Checker_lookupIdent(c, "char"))
        default:
            Checker_error(c, ast.Expr_pos(expr), "unreachable")
            return NULL
        }
    case ast.EXPR_CALL:
        {
            var func *ast.Expr = expr.call.func
            var type *ast.Expr = Checker_checkExpr(c, func)
            if type.kind == ast.EXPR_STAR {
                type = type.star.x
            }
            if type.kind == ast.TYPE_BUILTIN {
                var i int = 0
                for ; expr.call.args[i]; i++ {
                    Checker_checkExpr(c, expr.call.args[i])
                }
                if type.builtin.variadic {
                    assert(i == type.builtin.nargs)
                } else {
                    assert(i >= type.builtin.nargs)
                }
                return NULL
            } else if type.kind == ast.TYPE_FUNC {
                var j int = 0
                for var i int = 0; expr.call.args[i]; i++ {
                    var param *ast.Decl = type.func.params[j]
                    if param == NULL {
                        Checker_error(c, ast.Expr_pos(expr), "too many args")
                        break
                    }
                    var type *ast.Expr = Checker_checkExpr(c, expr.call.args[i])
                    assert(param.kind == ast.DECL_FIELD)
                    if !areAssignable(param.field.type, type) {
                        Checker_error(c, ast.Expr_pos(expr), sys.sprintf("not assignable: %s and %s", typeString(param.field.type), typeString(type)))
                    }
                    if param.field.type.kind != ast.TYPE_ELLIPSIS {
                        j++
                    }
                }
                return type.func.result
            } else {
                Checker_error(c, ast.Expr_pos(expr), sys.sprintf("`%s` is not a func", exprString(expr.call.func)))
            }
        }
    case ast.EXPR_COMPOSITE_LIT:
        Checker_checkType(c, expr.composite.type)
        Checker_checkCompositeLit(c, expr)
        return expr.composite.type
    case ast.EXPR_CAST:
        Checker_checkType(c, expr.cast.type)
        Checker_checkExpr(c, expr.cast.expr)
        return expr.cast.type
    case ast.EXPR_IDENT:
        Checker_resolve(c, c.pkg.scope, expr)
        return Checker_checkIdent(c, expr)
    case ast.EXPR_INDEX:
        {
            var type *ast.Expr = Checker_checkExpr(c, expr.index.x)
            switch type.kind {
            case ast.TYPE_ARRAY:
                type = type.array.elt
                break
            case ast.EXPR_STAR:
                type = type.star.x
                break
            default:
                Checker_error(c, ast.Expr_pos(expr), sys.sprintf("indexing a non-array or pointer `%s`", typeString(type)))
                break
            }
            var typ2 *ast.Expr = Checker_checkExpr(c, expr.index.index)
            typ void(typ2)
            return type
        }
    case ast.EXPR_PAREN:
        return Checker_checkExpr(c, expr.paren.x)
    case ast.EXPR_SELECTOR:
        {
            var type *ast.Expr = Checker_checkExpr(c, expr.selector.x)
            if type == NULL {
                var x *ast.Expr = expr.selector.x
                assert(x.kind == ast.EXPR_IDENT)
                var decl *ast.Decl = x.ident.obj.decl
                assert(decl.kind == ast.DECL_IMPORT)
                var oldScope *ast.Scope = c.pkg.scope
                c.pkg.scope = x.ident.obj.data
                type = Checker_checkExpr(c, expr.selector.sel)
                c.pkg.scope = oldScope
                expr.selector.tok = token.DOLLAR
                return type
            }
            if type.kind == ast.EXPR_STAR {
                expr.selector.tok = token.ARROW
                type = type.star.x
            } else {
                assert(expr.selector.tok != token.ARROW)
            }
            var field *ast.Decl = getStructFieldByName(type, expr.selector.sel)
            if field == NULL {
                Checker_error(c, ast.Expr_pos(expr), sys.sprintf("no member named '%s' in '%s'", exprString(expr.selector.sel), typeString(type)))
            }
            expr.selector.sel.ident.obj = field.field.name.ident.obj
            return field.field.type
        }
    case ast.EXPR_SIZEOF:
        Checker_checkType(c, expr.sizeof_.x)
        return Checker_lookupIdent(c, "u64")
    case ast.EXPR_STAR:
        {
            var type *ast.Expr = Checker_checkExpr(c, expr.star.x)
            switch type.kind {
            case ast.EXPR_STAR:
                return type.star.x
            case ast.TYPE_ARRAY:
                return type.array.elt
            default:
                Checker_error(c, ast.Expr_pos(expr), sys.sprintf("derefencing a non-pointer `%s`", typeString(type)))
                return NULL
            }
        }
    case ast.EXPR_TERNARY:
        {
            var t1 *ast.Expr = Checker_checkExpr(c, expr.ternary.cond)
            assert(isArithmetic(t1))
            var t2 *ast.Expr = expr.ternary.x ? Checker_checkExpr(c, expr.ternary.x) : t1
            var t3 *ast.Expr = Checker_checkExpr(c, expr.ternary.y)
            if !areComparable(t2, t3) {
                Checker_error(c, ast.Expr_pos(expr), "not comparable")
            }
            return t2
        }
    case ast.EXPR_UNARY:
        {
            var type *ast.Expr = Checker_checkExpr(c, expr.unary.x)
            switch expr.unary.op {
            case token.AND:
                if !isLhs(expr.unary.x) {
                    Checker_error(c, ast.Expr_pos(expr), sys.sprintf("invalid lvalue `%s`", exprString(expr.unary.x)))
                }
                return makePtr(type)
            case token.LAND:
                return makePtr(type)
            default:
                break
            }
            return type
        }
    default:
        Checker_error(c, ast.Expr_pos(expr), "unknown expr")
        return NULL
    }
}

fun Checker_checkStmt(c *Checker, stmt *ast.Stmt) {
    switch stmt.kind {
    case ast.STMT_ASSIGN:
        {
            if !isLhs(stmt.assign.x) {
                Checker_error(c, ast.Stmt_pos(stmt), sys.sprintf("invalid lvalue `%s`", exprString(stmt.assign.x)))
            }
            var a *ast.Expr = Checker_checkExpr(c, stmt.assign.x)
            if a.is_const {
                Checker_error(c, ast.Stmt_pos(stmt), "cannot assign to const var")
            }
            var b *ast.Expr = Checker_checkExpr(c, stmt.assign.y)
            if !areAssignable(a, b) {
                Checker_error(c, ast.Stmt_pos(stmt), sys.sprintf("not assignable: `%s` and `%s`", typeString(a), typeString(b)))
            }
        }
        break
    case ast.STMT_BLOCK:
        Checker_openScope(c)
        for var i int = 0; stmt.block.stmts[i]; i++ {
            Checker_checkStmt(c, stmt.block.stmts[i])
        }
        Checker_closeScope(c)
        break
    case ast.STMT_DECL:
        Checker_checkDecl(c, stmt.decl.decl)
        break
    case ast.STMT_EXPR:
        Checker_checkExpr(c, stmt.expr.x)
        break
    case ast.STMT_EMPTY:
        break
    case ast.STMT_IF:
        Checker_checkExpr(c, stmt.if_.cond)
        Checker_checkStmt(c, stmt.if_.body)
        if stmt.if_.else_ {
            Checker_checkStmt(c, stmt.if_.else_)
        }
        break
    case ast.STMT_ITER:
        if stmt.iter.init || stmt.iter.post {
            Checker_openScope(c)
        }
        if stmt.iter.init {
            Checker_checkStmt(c, stmt.iter.init)
        }
        if stmt.iter.cond {
            Checker_checkExpr(c, stmt.iter.cond)
        }
        if stmt.iter.post {
            Checker_checkStmt(c, stmt.iter.post)
        }
        Checker_checkStmt(c, stmt.iter.body)
        if stmt.iter.init || stmt.iter.post {
            Checker_closeScope(c)
        }
        break
    case ast.STMT_JUMP:
        break
    case ast.STMT_LABEL:
        Checker_checkStmt(c, stmt.label.stmt)
        break
    case ast.STMT_POSTFIX:
        Checker_checkExpr(c, stmt.postfix.x)
        break
    case ast.STMT_RETURN:
        if stmt.return_.x {
            var a *ast.Expr = c.result
            var b *ast.Expr = Checker_checkExpr(c, stmt.return_.x)
            if a == NULL {
                Checker_error(c, ast.Stmt_pos(stmt), "returning value in void function")
            }
            if !areAssignable(a, b) {
                Checker_error(c, ast.Stmt_pos(stmt), sys.sprintf("not returnable: %s and %s", typeString(a), typeString(b)))
            }
        }
        break
    case ast.STMT_SWITCH:
        {
            var type1 *ast.Expr = Checker_checkExpr(c, stmt.switch_.tag)
            for var i int = 0; stmt.switch_.stmts[i]; i++ {
                var clause *ast.Stmt = stmt.switch_.stmts[i]
                assert(clause.kind == ast.STMT_CASE)
                for var j int = 0; clause.case_.exprs && clause.case_.exprs[j]; j++ {
                    var type2 *ast.Expr = Checker_checkExpr(c, clause.case_.exprs[j])
                    if !areComparable(type1, type2) {
                        Checker_error(c, ast.Stmt_pos(stmt), sys.sprintf("not comparable: %s and %s", typeString(type1), typeString(type2)))
                    }
                }
                for var j int = 0; clause.case_.stmts[j]; j++ {
                    Checker_checkStmt(c, clause.case_.stmts[j])
                }
            }
        }
        break
    default:
        Checker_error(c, ast.Stmt_pos(stmt), "unknown stmt")
    }
}

fun Checker_checkImport(c *Checker, imp *ast.Decl) *Package {
    var path *char = constant_stringVal(imp.imp.path)
    var pkg *Package = NULL
    utils.Map_get(&c.info.imports, path, &pkg)
    if pkg == NULL {
        pkg = check(c.conf, path, c.fset, NULL, c.info)
        assert(utils.Map_get(&c.info.imports, path, NULL))
    }
    imp.imp.name = makeIdent(pkg.scope.pkg)
    Checker_declare(c, imp, pkg.scope, c.pkg.scope, ast.ObjKind_PKG, imp.imp.name)
    return pkg
}

fun Checker_checkDecl(c *Checker, decl *ast.Decl) {
    switch decl.kind {
    case ast.DECL_FUNC:
        {
            Checker_checkType(c, decl.func.type)
            if c.conf.ignoreFuncBodies || !decl.func.body {
                break
            }
            Checker_openScope(c)
            var type *ast.Expr = decl.func.type
            for var i int = 0; type.func.params && type.func.params[i]; i++ {
                var param *ast.Decl = type.func.params[i]
                assert(param.kind == ast.DECL_FIELD)
                if param.field.name {
                    Checker_declare(c, param, NULL, c.pkg.scope, ast.ObjKind_VAL, param.field.name)
                }
            }
            c.result = decl.func.type.func.result
            for var i int = 0; decl.func.body.block.stmts[i]; i++ {
                Checker_checkStmt(c, decl.func.body.block.stmts[i])
            }
            c.result = NULL
            Checker_closeScope(c)
            break
        }
    case ast.DECL_PRAGMA:
        break
    case ast.DECL_TYPEDEF:
        switch decl.typedef_.type.kind {
        case ast.TYPE_ENUM:
            decl.typedef_.type.enum_.name = decl.typedef_.name
            break
        case ast.TYPE_STRUCT:
            decl.typedef_.type.struct_.name = decl.typedef_.name
            break
        default:
            break
        }
        Checker_checkType(c, decl.typedef_.type)
        break
    case ast.DECL_VALUE:
        {
            var valType *ast.Expr = NULL
            if decl.value.type != NULL {
                Checker_checkType(c, decl.value.type)
            }
            if decl.value.value != NULL {
                if decl.value.value.kind == ast.EXPR_COMPOSITE_LIT {
                    if decl.value.value.composite.type == NULL {
                        decl.value.value.composite.type = decl.value.type
                    } else {
                        Checker_checkType(c, decl.value.value.composite.type)
                    }
                    valType = Checker_checkCompositeLit(c, decl.value.value)
                } else {
                    valType = Checker_checkExpr(c, decl.value.value)
                }
            }
            if decl.value.type == NULL {
                decl.value.type = valType
            }
            if valType != NULL {
                var varType *ast.Expr = decl.value.type
                if !areAssignable(varType, valType) {
                    Checker_error(c, decl.pos, sys.sprintf("not assignable %s and %s", typeString(varType), typeString(valType)))
                }
            }
            Checker_declare(c, decl, NULL, c.pkg.scope, ast.ObjKind_VAL, decl.value.name)
            break
        }
    default:
        Checker_error(c, decl.pos, "unreachable")
    }
}

fun Checker_checkFile(c *Checker, file *ast.File) {
    for var i int = 0; file.imports[i] != NULL; i++ {
        var pkg *Package = Checker_checkImport(c, file.imports[i])
        utils.Slice_append(&c.pkg.imports, &pkg)
    }
    for var i int = 0; file.decls[i] != NULL; i++ {
        var kind ast.ObjKind = ast.ObjKind_BAD
        switch file.decls[i].kind {
        case ast.DECL_FUNC:
            kind = ast.ObjKind_FUN
            break
        case ast.DECL_TYPEDEF:
            kind = ast.ObjKind_TYP
            break
        default:
            break
        }
        if kind {
            Checker_declare(c, file.decls[i], NULL, c.pkg.scope, kind, file.decls[i].typedef_.name)
        }
    }
    for var i int = 0; file.decls[i] != NULL; i++ {
        Checker_checkDecl(c, file.decls[i])
    }
}

fun newInfo() *Info {
    var info Info = {
        imports: utils.Map_make(sizeof(*Package)),
    }
    return &&info
}

fun checkFile(conf *Config, path *const char, fset *token.FileSet, file *ast.File, info *Info) *Package {
    if info {
        var pkg *Package = NULL
        utils.Map_get(&info.imports, path, &pkg)
        if pkg {
            return pkg
        }
    } else {
        info = newInfo()
    }
    var pkg Package = {
        path: sys.strdup(path),
        name: file.name ? sys.strdup(file.name.ident.name) : NULL,
        scope: file.scope,
        imports: {
            size: sizeof(*Package),
        },
    }
    var c Checker = {
        info: info,
        fset: fset,
        conf: conf,
        pkg: &&pkg,
    }
    utils.Map_set(&info.imports, path, &c.pkg)
    Checker_checkFile(&c, file)
    return c.pkg
}

fun check(conf *Config, path *const char, fset *token.FileSet, files **ast.File, info *Info) *Package {
    if files == NULL {
        files = parser.parseDir(fset, path, universe(), NULL)
    }
    assert(files[0] && !files[1])
    return checkFile(conf, path, fset, files[0], info)
}

var _universe *ast.Scope = NULL

var predeclaredTypes []const Basic = {
    INVALID: {
        INVALID,
        0,
        "invalid type",
    },
    VOID: {
        VOID,
        IS_UNTYPED,
        "void",
    },
    BOOL: {
        BOOL,
        IS_BOOLEAN,
        "bool",
    },
    CHAR: {
        CHAR,
        IS_INTEGER,
        "char",
    },
    INT: {
        INT,
        IS_INTEGER,
        "int",
    },
    INT8: {
        INT8,
        IS_INTEGER,
        "i8",
    },
    INT16: {
        INT16,
        IS_INTEGER,
        "i16",
    },
    INT32: {
        INT32,
        IS_INTEGER,
        "i32",
    },
    INT64: {
        INT64,
        IS_INTEGER,
        "i64",
    },
    UINT: {
        UINT,
        IS_INTEGER | IS_UNSIGNED,
        "uint",
    },
    UINT8: {
        UINT8,
        IS_INTEGER | IS_UNSIGNED,
        "u8",
    },
    UINT16: {
        UINT16,
        IS_INTEGER | IS_UNSIGNED,
        "u16",
    },
    UINT32: {
        UINT32,
        IS_INTEGER | IS_UNSIGNED,
        "u32",
    },
    UINT64: {
        UINT64,
        IS_INTEGER | IS_UNSIGNED,
        "u64",
    },
    UINTPTR: {
        UINTPTR,
        IS_INTEGER | IS_UNSIGNED,
        "uintptr",
    },
    FLOAT32: {
        FLOAT32,
        IS_FLOAT,
        "float",
    },
    FLOAT64: {
        FLOAT64,
        IS_FLOAT,
        "double",
    },
    VOID_POINTER: {
        VOID_POINTER,
        0,
        "voidptr",
    },
}

fun defPredeclaredTypes() {
    for var i int = 0; i < UNSAFE_POINTER; i++ {
        if !predeclaredTypes[i].name {
            continue
        }
        var obj *ast.Object = ast.newObject(ast.ObjKind_TYP, predeclaredTypes[i].name)
        var name ast.Expr = {
            kind: ast.EXPR_IDENT,
            ident: {
                name: sys.strdup(predeclaredTypes[i].name),
                obj: obj,
            },
        }
        var type ast.Expr = {
            kind: ast.TYPE_NATIVE,
            native: {
                kind: predeclaredTypes[i].kind,
                info: predeclaredTypes[i].info,
                name: sys.strdup(predeclaredTypes[i].name),
            },
        }
        var decl ast.Decl = {
            kind: ast.DECL_TYPEDEF,
            typedef_: {
                name: &&name,
                type: &&type,
            },
        }
        obj.decl = &&decl
        assert(ast.Scope_insert(_universe, obj) == NULL)
    }
}

typ builtinId enum {
    _assert
    _mapmake
    _panic
    _print
    numBuiltinIds
}

typ exprKind enum {
    expression
    statement
}

var predeclareFuncs []struct {
    name *char
    nargs int
    variadic bool
    kind exprKind
} = {
    _assert: {
        "assert",
        1,
        false,
        statement,
    },
    _mapmake: {
        "mapmake",
        1,
        false,
        expression,
    },
    _panic: {
        "panic",
        1,
        false,
        statement,
    },
    _print: {
        "print",
        1,
        false,
        statement,
    },
}

fun defPredeclaredFuncs() {
    for var i int = 0; i < numBuiltinIds; i++ {
        var name ast.Expr = {
            kind: ast.EXPR_IDENT,
            ident: {
                name: sys.strdup(predeclareFuncs[i].name),
            },
        }
        var type ast.Expr = {
            kind: ast.TYPE_BUILTIN,
            builtin: {
                name: sys.strdup(predeclareFuncs[i].name),
                nargs: predeclareFuncs[i].nargs,
                variadic: predeclareFuncs[i].variadic,
                isExpr: predeclareFuncs[i].kind == expression,
            },
        }
        var decl ast.Decl = {
            kind: ast.DECL_FUNC,
            func: {
                name: &&name,
                type: &&type,
            },
        }
        var obj *ast.Object = ast.newObject(ast.ObjKind_FUN, name.ident.name)
        obj.decl = &&decl
        obj.decl.func.name.ident.obj = obj
        assert(ast.Scope_insert(_universe, obj) == NULL)
    }
}

var predeclareConsts []struct {
    name *char
    type *char
    value int
} = {
    {
        "NULL",
        "voidptr",
        0,
    },
    {
        "false",
        "bool",
        0,
    },
    {
        "true",
        "bool",
        1,
    },
    {
        NULL,
    },
}

fun defPredeclaredConsts() {
    for var i int = 0; predeclareConsts[i].name; i++ {
        var obj *ast.Object = ast.Scope_lookup(_universe, predeclareConsts[i].type)
        var name ast.Expr = {
            kind: ast.EXPR_IDENT,
            ident: {
                name: sys.strdup(predeclareConsts[i].name),
            },
        }
        var expr ast.Expr = {
            kind: ast.EXPR_CONST,
            const_: {
                name: sys.strdup(predeclareConsts[i].name),
                type: obj.decl.typedef_.name,
                value: predeclareConsts[i].value,
            },
        }
        var decl ast.Decl = {
            kind: ast.DECL_VALUE,
            value: {
                name: &&name,
                type: obj.decl.typedef_.name,
                value: &&expr,
            },
        }
        obj = ast.newObject(ast.ObjKind_CON, name.ident.name)
        obj.decl = &&decl
        obj.decl.value.name.ident.obj = obj
        assert(ast.Scope_insert(_universe, obj) == NULL)
    }
}

fun universe() *ast.Scope {
    if _universe == NULL {
        _universe = ast.Scope_new(NULL)
        defPredeclaredTypes()
        defPredeclaredConsts()
        defPredeclaredFuncs()
    }
    return _universe
}
