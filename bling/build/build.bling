package build

import "C"
import "bling/ast"
import "bling/emitter"
import "bling/parser"
import "bling/token"
import "bling/types"
import "bytes"
import "io/ioutil"
import "os"
import "paths"
import "runtime"
import "subc/cemitter"
import "subc/cparser"

let AR_PATH const string = "/usr/bin/ar"
let CC_PATH const string = "/usr/bin/cc"
let GEN_PATH const string = "gen"
let INCL_PATH const string = "."
let VERBOSE const bool = true

fun printStrArray(s []string) {
    for var i = 0; i < len(s); i++ {
        if i {
            C.printf(" ")
        }
        C.printf("%s", s[i])
    }
    C.printf("\n")
}

fun execute(cmd *[]string) {
    if VERBOSE {
        printStrArray(*cmd)
    }
    let args = runtime.nilArray(typ *runtime.Slice(cmd), sizeof(string))
    let code = os.exec(args, nil)
    if code != 0 {
        panic(C.sprintf("- failed with code %d", code))
    }
    delete(args)
}

fun mkdirForFile(path string) {
    let dir = paths.dir(path)
    os.mkdirAll(dir, 0755, nil)
    delete(dir)
}

typ Package struct {
    path string
    pkg *types.Package
    genPath string
    hPath string
    cPath string
    objPath string
    objPaths []string
    libPath string
    libModTime os.Time
    srcModTime os.Time
    deps []*Package
    isCmd bool
}

typ Builder struct {
    fset *token.FileSet
    info *types.Info
    force bool
    conf types.Config
    pkgs map *Package
}

fun newBuilder(fset *token.FileSet) *Builder {
    let builder Builder = {
        fset: fset,
        info: types.newInfo(),
        force: false,
        pkgs: makemap(*Package),
    }
    return &&builder
}

fun genObj(b *Builder, dst string, src string) {
    let cmd = []string{}
    append(cmd, CC_PATH)
    append(cmd, typ string("-g"))
    append(cmd, typ string("-I"))
    append(cmd, INCL_PATH)
    append(cmd, typ string("-c"))
    append(cmd, typ string("-o"))
    append(cmd, dst)
    append(cmd, src)
    mkdirForFile(dst)
    execute(&cmd)
}

fun getFileModTime(path string) os.Time {
    let err *runtime.Error = nil
    if let info = os.stat(path, &err); err == nil {
        let t = info.modTime()
        info.free()
        return t
    }
    return 0
}

fun getSrcModTime(path string) os.Time {
    var t os.Time = 0
    let files = ioutil.readDir(path, nil)
    for var i = 0; i < len(files); i++ {
        let file = files[i]
        let modTime = file.modTime()
        if bytes.hasSuffix(file.name(), ".bling") {
            if t < modTime {
                t = modTime
            }
        }
        file.free()
    }
    delete(files)
    return t
}

fun buildCFile(b *Builder, cFile *os.FileInfo) *os.FileInfo {
    let src = cFile.name()
    var dst string = nil
    {
        let i = bytes.lastIndexByte(src, '.')
        let base = runtime.strndup(src, i)
        dst = C.sprintf("%s/%s.o", GEN_PATH, base)
        delete(base)
    }
    let err *runtime.Error = nil
    var objFile = os.stat(dst, &err)
    let doBuild = objFile == nil || cFile.modTime() > objFile.modTime()
    if b.force || doBuild {
        genObj(b, dst, src)
        objFile.free()
        objFile = os.stat(dst, &err)
    }
    return objFile
}

fun newPackage(b *Builder, path string) Package {
    let base = paths.base(path)
    let files = parser.parseDir(b.fset, path, nil)
    let typesPkg = types.check(&b.conf, path, b.fset, files, b.info)
    let isCmd = typesPkg.name ? runtime.streq(typesPkg.name, "main") : false
    let genPath = paths.join2(GEN_PATH, path)
    var libPath = nil
    if isCmd {
        libPath = C.sprintf("%s/%s", genPath, base)
    } else {
        libPath = C.sprintf("%s/%s.a", genPath, base)
    }
    let pkg = Package{
        path: runtime.strdup(path),
        pkg: typesPkg,
        genPath: genPath,
        hPath: C.sprintf("%s/%s.h", genPath, base),
        cPath: C.sprintf("%s/%s.c", genPath, base),
        objPath: C.sprintf("%s/%s.o", genPath, base),
        libPath: libPath,
        libModTime: getFileModTime(libPath),
        srcModTime: getSrcModTime(path),
        deps: []*Package{},
        isCmd: isCmd,
    }
    delete(base)
    for var i = 0; i < len(pkg.pkg.imports); i++ {
        let dep = _buildPackage(b, pkg.pkg.imports[i].path)
        append(pkg.deps, dep)
        if pkg.srcModTime < dep.srcModTime {
            pkg.srcModTime = dep.srcModTime
        }
    }
    return pkg
}

fun emitInclude(e *emitter.Emitter, path string) {
    let s = C.sprintf("#include \"%s\"\n", path)
    e.emitString(s)
    delete(s)
}

fun writeFile(path string, out string) {
    mkdirForFile(path)
    ioutil.writeFile(path, runtime.charstobytes(out), 0644, nil)
}

fun genHeader(b *Builder, pkg *Package) {
    let e = emitter.Emitter{}
    e.emitString("#pragma once")
    e.emitNewline()
    emitInclude(&e, "bootstrap/bootstrap.h")
    for var i = 0; i < len(pkg.deps); i++ {
        emitInclude(&e, pkg.deps[i].hPath)
    }
    cemitter.emitHeader(&e, pkg.pkg)
    let out = e.string()
    writeFile(pkg.hPath, out)
    delete(out)
}

fun getCFile(b *Builder, pkg *Package) {
    let e = emitter.Emitter{}
    emitInclude(&e, pkg.hPath)
    cemitter.emitBody(&e, pkg.pkg)
    let out = e.string()
    writeFile(pkg.cPath, out)
    delete(out)
}

fun buildCPackage(b *Builder, path string) *Package {
    let pkg = newPackage(b, path)
    let files = ioutil.readDir(path, nil)
    for var i = 0; i < len(files); i++ {
        let checkTime = false
        let file = files[i]
        let modTime = file.modTime()
        let name = file.name()
        if bytes.hasSuffix(name, ".bling") || bytes.hasSuffix(name, ".c") {
            if pkg.srcModTime < modTime {
                pkg.srcModTime = modTime
            }
        }
    }
    if b.force || pkg.srcModTime > pkg.libModTime {
        genHeader(b, &pkg)
        let cmd = []string{}
        append(cmd, AR_PATH)
        append(cmd, typ string("rsc"))
        append(cmd, pkg.libPath)
        for var i = 0; i < len(files); i++ {
            let file = files[i]
            if bytes.hasSuffix(file.name(), ".c") {
                let obj = buildCFile(b, file)
                append(cmd, file.name())
            }
        }
        mkdirForFile(pkg.libPath)
        execute(&cmd)
        pkg.libModTime = getFileModTime(pkg.libPath)
    }
    for var i = 0; i < len(files); i++ {
        files[i].free()
    }
    delete(files)
    return &&pkg
}

fun buildBlingPackage(b *Builder, path string) *Package {
    let pkg = newPackage(b, path)
    if b.force || pkg.srcModTime > pkg.libModTime {
        mkdirForFile(pkg.libPath)
        genHeader(b, &pkg)
        getCFile(b, &pkg)
        genObj(b, pkg.objPath, pkg.cPath)
        if pkg.isCmd {
            let cmd = []string{}
            append(cmd, CC_PATH)
            append(cmd, typ string("-g"))
            append(cmd, typ string("-o"))
            append(cmd, pkg.libPath)
            append(cmd, pkg.objPath)
            var key string
            for let iter = runtime.newMapIter(&b.pkgs); runtime.MapIter_next(&iter, &key); {
                append(cmd, b.pkgs[key].libPath)
            }
            execute(&cmd)
        } else {
            let cmd = []string{}
            append(cmd, AR_PATH)
            append(cmd, typ string("rsc"))
            append(cmd, pkg.libPath)
            append(cmd, pkg.objPath)
            execute(&cmd)
        }
    }
    return &&pkg
}

fun _buildPackage(b *Builder, path string) *Package {
    var pkg = b.pkgs[path]
    if pkg {
        return pkg
    }
    if runtime.streq(path, "C") {
        pkg = buildCPackage(b, path)
    } else {
        pkg = buildBlingPackage(b, path)
    }
    b.pkgs[path] = pkg
    return pkg
}

fun buildPackage(b *Builder, path string) {
    let clean = paths.clean(path)
    _buildPackage(b, "runtime")
    _buildPackage(b, clean)
    delete(clean)
}
